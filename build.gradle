plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'sfn'
version '1.0.107'

java.sourceCompatibility = JavaVersion.VERSION_11
java.targetCompatibility = JavaVersion.VERSION_11

java {
    withSourcesJar()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

    // gson
    api group: "com.google.code.gson", name: "gson", version: "2.7"
    // excel
    api group: "org.apache.poi", name: "poi", version: "4.1.0"
    api group: "org.apache.poi", name: "poi-ooxml", version: "4.1.0"
}

repositories {
    maven {
        url = "https://repo.smartfoodnet.com/repository/maven/"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = 'excel-module'
            version = version

            from components.java
        }
    }
}

publishing {
    repositories {
        maven {
            String authEncoded = System.getenv('NEXUS_AUTH')
            String userId = ''
            String pwd = ''

            if (authEncoded != null) {
                String authDecoded = new String(authEncoded.decodeBase64())
                userId = authDecoded.split(":")[0]
                pwd = authDecoded.split(":")[1]
            }

            url = 'https://repo.my.host/content/repositories/releases/'

            credentials {
                username = userId
                password = pwd
            }
        }
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.filter { it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
}